plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
}

def buildNumber = System.getenv("RELEASE_VERSION")?: "DEV-SNAPSHOT"

group = 'com.github.hmcts'
version = buildNumber

def gitRepo = "https://github.com/hmcts/fortify-client"

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "http://www.opensource.org/licenses/mit-license.php"
            distribution "repo"
        }
    }

    scm {
        url "https://github.com/hmcts/fortify-client"
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

jar {
    archiveBaseName = "fortify-client"
}

tasks.register('fatJar', Jar) {
    archiveClassifier = 'all'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest {
        attributes 'Implementation-Title': 'fortify-client',
                'Implementation-Version': '1.0',
                'Main-Class': 'uk.gov.hmcts.fortifyclient.FortifyClientMainApp'
    }

    into('/lib') {
        from 'lib'
    }

    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

dependencies {
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.5.25'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.20.0'
    implementation group: 'commons-io', name: 'commons-io', version: '2.21.0'

    testImplementation 'org.hamcrest:hamcrest:3.0'
    testImplementation(platform("org.junit:junit-bom:6.0.2"))
    testImplementation 'org.junit.jupiter:junit-jupiter'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

compileJava {
    options.annotationProcessorPath = configurations.annotationProcessor
}

test {
    useJUnitPlatform()
}

artifacts {
    archives fatJar
}

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            artifact fatJar
            groupId = project.group
            artifactId = 'fortify-client'
            version = project.version

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Fortify Integration Framework')
                root.appendNode('name', 'Fortify Client')
                root.appendNode('url', gitRepo)
                root.children().last() + pomConfig
            }
        }
    }
    repositories {
        maven {
            name = "AzureArtifacts"
            url = uri("https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1")
            credentials {
                username = System.getenv("AZURE_DEVOPS_ARTIFACT_USERNAME")
                password = System.getenv("AZURE_DEVOPS_ARTIFACT_TOKEN")
            }
        }
    }
}
