plugins {
    id 'java-library'
    id 'maven-publish'
}

apply plugin: 'java'

def buildNumber = System.getenv("RELEASE_VERSION")?.replace("refs/tags/", "") ?: "1.0.0"

group 'com.github.hmcts'

version buildNumber

def gitRepo = "https://github.com/hmcts/fortify-client"

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "http://www.opensource.org/licenses/mit-license.php"
            distribution "repo"
        }
    }

    scm {
        url "https://github.com/hmcts/fortify-client"
    }
}

// tag::repositories[]
repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'https://jitpack.io'
    }
}
// end::repositories[]

jar {
    archiveBaseName = "fortify-client"
}

tasks.register('fatJar', Jar) {
    archiveClassifier = 'all'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest {
        attributes 'Implementation-Title': 'fortify-client',
                'Implementation-Version': '1.0',
                'Main-Class': 'uk.gov.hmcts.fortifyclient.FortifyClientMainApp'
    }

    into('/lib') {
        from 'lib'
    }

    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

wrapper.gradleVersion = '2.10'

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.5.3'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.14.0'
    implementation group: 'commons-io', name: 'commons-io', version: '2.15.1'
}
// end::dependencies[]

compileJava {
    options.annotationProcessorPath = configurations.annotationProcessor
}

test {
    useJUnitPlatform()
}

artifacts {
    archives fatJar
}

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            artifact fatJar
            groupId project.group
            artifactId 'fortify-client'
            version project.version

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Fortify Integration Framework')
                root.appendNode('name', 'Fortify Client')
                root.appendNode('url', gitRepo)
                root.children().last() + pomConfig
            }
        }
    }
}
